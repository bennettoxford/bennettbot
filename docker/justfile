# Load .env files by default; the project .env lives one directory up

set dotenv-load := true
set dotenv-filename := ".env"

# used by docker compose

export DEV_USERID := `id -u`
export DEV_GROUPID := `id -g`

# set versions

# list available commands
default:
    @{{ just_executable() }} --list

_dotenv:
    {{ just_executable() }} --justfile {{ justfile_directory() }}/../justfile _dotenv

build env="dev": _dotenv
    #!/usr/bin/env bash

    # set build args for prod builds
    export BUILD_DATE=$(date -u +'%y-%m-%dT%H:%M:%SZ')
    export GITREF=$(git rev-parse --short HEAD)

    # build the thing
    docker compose build --pull {{ env }}

# run python checks
check: build
    docker compose run --rm dev /app/docker/entrypoints/check.sh

# run tests in docker container
test *args="": build
    docker compose run --rm test /app/docker/entrypoints/test.sh {{ args }}

# run command in dev container
run-cmd *args="bash": build
    docker compose run --rm dev {{ args }}

# run all services
run-all: build
    docker compose up --detach dev_bot
    docker compose up --detach dev_dispatcher
    docker compose up --detach dev_webserver

# restart all services
restart:
    docker compose restart dev_bot
    docker compose restart dev_dispatcher
    docker compose restart dev_webserver

# stop all dev containers
stop-all:
    docker compose stop dev_bot
    docker compose stop dev_dispatcher
    docker compose stop dev_webserver

# stop all dev containers
rm-all: stop-all
    docker compose rm dev_bot
    docker compose rm dev_dispatcher
    docker compose rm dev_webserver

lint:
    docker run --rm -i ghcr.io/hadolint/hadolint:v2.14.0-alpine < Dockerfile

# exec command in existing dev container
exec *args="bash":
    docker compose exec dev {{ args }}

# stop and remove all project containers
clean:
    #!/bin/bash
    set -eux
    . .env
    docker compose down
    docker container prune --force --filter label=com.docker.compose.project=$COMPOSE_PROJECT_NAME
